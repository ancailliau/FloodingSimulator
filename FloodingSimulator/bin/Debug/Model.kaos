# Root Goals

declare goal
  id locals_warned_when_risk_imminent
  name "Achieve [LocalsWarnedWhenRiskImminent]"
  rds .65
  refinedby radar_depth_acquired_every_5_seconds, speed_acquired_every_5_seconds, critical_detected, locals_warned
end

declare goal
  id avoid_false_warning
  name "Avoid [FalseWarning]"
  refinedby acquired_radar_depth_accurate, acquired_ultrasound_speed_accurate
end

# Goals

declare goal
	id radar_depth_acquired_every_5_seconds
	name "Achieve [DepthAcquiredEvery5SecondsByRadar]"
	formalspec when not radarDepthAcquired() then sooner-or-later, before 5 seconds, radarDepthAcquired()
  assignedto radarDepthSensor
  obstructedby depth_not_acquired
end

declare goal
  id acquired_radar_depth_accurate
  name "Maintain [AcquiredRadarDepthAccurate]"
  formalspec when radarDepthAcquired() and acquiredDepthCritical() then sooner-or-later, before 3 seconds, depthAccurate()
  assignedto radarDepthSensor
  obstructedby acquired_depth_not_accurate
end

declare goal
	id speed_acquired_every_5_seconds
	name "Achieve [SpeedAcquiredEvery5SecondsByUltrasound]"
	formalspec when not speedAcquiredByUltrasound() then sooner-or-later, before 5 seconds, speedAcquiredByUltrasound()
  assignedto ultrasoundSensor
  obstructedby speed_not_acquired
end

declare goal
  id acquired_ultrasound_speed_accurate
  name "Maintain [UltrasoundSpeedAccurate]"
  formalspec when speedAcquiredByUltrasound() and acquiredSpeedCritical() then sooner-or-later, before 3 seconds, speedAccurate()
  assignedto ultrasoundSensor
  obstructedby acquired_speed_not_accurate
end

declare goal
  id locals_warned
  name "Achieve [LocalsWarnedByPhoneWhenLevelsCritical]"
  formalspec when acquiredDepthCritical() and acquiredSpeedCritical()
             then sooner-or-later, before 5 seconds, localsWarnedByPhone()
  assignedto voiceWarner
  obstructedby locals_not_warned
end

declare goal
  id critical_detected
  name "Achieve [LevelsCriticalDetectedWhenDataAcquired]"
  assignedto fds
#  obstructedby critical_not_detected
end

# Root obstacles

declare obstacle
  id depth_not_acquired
  name "Depth Not Acquired"
  refinedby depth_broken
end

declare obstacle
  id acquired_depth_not_accurate
  name "Acquired Depth Not Accurate"
  refinedby dusty_environment
  refinedby false_echo
end

declare obstacle
  id speed_not_acquired
  name "Speed Not Acquired"
  refinedby ultrasound_broken
end

declare obstacle
  id acquired_speed_not_accurate
  name "Acquired Speed Not Accurate"
  refinedby ultrasound_distortion
end

declare obstacle
  id locals_not_warned
  name "Locals Not Warned In Time"
  refinedby voice_down
  refinedby gsm_down
end

#declare obstacle
#  id critical_not_detected
#  name "Critical Levels Not Detected"
#  refinedby low_refresh_rate_in_emergency
#end

# Obstacles

declare obstacle
  id dusty_environment
  name "Dusty Environment"
  # formalspec sooner-or-later (radarDepthAcquired() and acquiredDepthCritical() and always, for more than 4 seconds, dustyEnvironment())
  formalspec sooner-or-later (always, for more than 4 seconds, dustyEnvironment())
end

declare obstacle
  id false_echo
  name "False Echo"
  # formalspec sooner-or-later (radarDepthAcquired() and acquiredDepthCritical() and always, for more than 4 seconds, falseEcho())
  formalspec sooner-or-later (always, for more than 4 seconds, falseEcho())
end

declare obstacle
  id depth_broken
  name "Radar Sensor Broken" 
  formalspec sooner-or-later (always, for more than 6 seconds, radarDepthSensorBroken())
end

declare obstacle
  id ultrasound_broken
  name "Ultrasound Sensor Broken"
 # formalspec sooner-or-later (not speedAcquiredByUltrasound() and always, for more than 6 seconds, ultrasoundSensorBroken())
  formalspec sooner-or-later (always, for more than 6 seconds, ultrasoundSensorBroken())
  resolvedby (substitution[speed_acquired_every_5_seconds]) speed_acquired_by_camera
 # resolvedby speed_acquired_by_camera
 # resolvedby speed_acquired_every_10_seconds
end

declare obstacle
  id ultrasound_distortion
  name "Distortion in Ultrasound Measure"
 # formalspec sooner-or-later (speedAcquiredByUltrasound() and acquiredSpeedCritical() and always, for more than 6 seconds, ultrasoundDistortion())
   formalspec sooner-or-later (always, for more than 6 seconds, ultrasoundDistortion())
  resolvedby (substitution[acquired_ultrasound_speed_accurate]) acquired_camera_speed_accurate
end

declare obstacle
  id noisy_image
  name "Noisy Image"
  formalspec sooner-or-later (always, for more than 6 seconds, noisyImage())
#  formalspec sooner-or-later (speedAcquiredByCamera() and acquiredSpeedCritical() and always, for more than 6 seconds, noisyImage())
#  resolvedby acquired_ultrasound_speed_accurate
end

declare obstacle
  id voice_down
  name "Voice Network Overloaded"
  formalspec sooner-or-later (acquiredDepthCritical() and acquiredSpeedCritical()
                              and always, for more than 5 seconds, voiceNetworkOverloaded())
  resolvedby (substitution[locals_warned]) locals_warned_by_sms
  resolvedby (substitution[locals_warned]) locals_warned_by_email
end

declare obstacle
  id gsm_down
  name "GSM Network Down"
  formalspec sooner-or-later (acquiredDepthCritical() and acquiredSpeedCritical()
                              and always, for more than 5 seconds, gsmNetworkDown())
  resolvedby (substitution[locals_warned]) locals_warned_by_email
end

#declare obstacle
#  id low_refresh_rate_in_emergency
#  name "LowAcquisionRateInEmergencySituation"
#  eps .1
#  resolvedby (substitution[locals_warned]) "Achieve [LocalsWarnedWhenRiskImminentAndEmergencySituation]"
#end

# Resolutions

declare goal
	id speed_acquired_every_10_seconds
	name "Achieve [SpeedAcquiredEvery10SecondsByUltrasound]"
	formalspec when not speedAcquiredByUltrasound() then sooner-or-later, before 10 seconds, speedAcquiredByUltrasound()
  assignedto ultrasoundSensor
end

declare goal
  id speed_acquired_by_camera
	name "Achieve [SpeedAcquiredEvery5SecondsByCamera]"
  refinedby images_acquired, tracers_identified, speed_acquired_from_tracers
  $ondeploy "DeployCamera"
  $onwithold "DeployUltrasound"
end

  declare goal
    id images_acquired
    name "Achieve [ImagesAcquiredEvery5Seconds]"
    assignedto camera
  end

  declare goal
    id tracers_identified
    name "Achieve [TracersIdentified]"
    assignedto cameraSpeedModule
  end

  declare goal
    id speed_acquired_from_tracers
    name "Achieve [SpeedAcquiredFromTracers]"
    assignedto cameraSpeedModule
  end

declare goal
  id acquired_camera_speed_accurate
  name "Maintain [CameraSpeedAccurate]"
  assignedto cameraSpeedModule
  obstructedby noisy_image
  $ondeploy "DeployCamera"
  $onwithold "DeployUltrasound"
end

declare goal
  id locals_warned_by_sms
  name "Achieve [LocalsWarnedBySMSWhenLevelsCritical]"
  formalspec when acquiredDepthCritical() and acquiredSpeedCritical()
             then sooner-or-later, before 5 seconds, localsWarnedBySMS()
  obstructedby gsm_down
  assignedto SMSWarner
  $ondeploy "DeploySMS"
  $onwithold "DeployPhone"
end

declare goal
  id locals_warned_by_email
  name "  "
  formalspec when acquiredDepthCritical() and acquiredSpeedCritical()
             then sooner-or-later, before 5 seconds, localsWarnedByEmail()
  assignedto EmailWarner
  $ondeploy "DeployEmail"
  $onwithold "DeployPhone"
end

# Predicates

declare predicate
	id radarDepthAcquired
	name "RadarDepthAcquired"
end

declare predicate
  id depthAccurate
  name "DepthAccurate"
end

declare predicate
	id speedAcquiredByUltrasound
	name "SpeedAcquiredByUltraSound"
end

declare predicate
	id speedAcquiredByCamera
	name "SpeedAcquiredByCamera"
end

declare predicate
  id speedAccurate
  name "SpeedAccurate"
end

declare predicate
  id acquiredDepthCritical
  name "AcquiredDepthCritical"
end

declare predicate
  id acquiredSpeedCritical
  name "AcquiredSpeedCritical"
end

declare predicate
  id localsWarnedByPhone
  name "LocalsWarnedByPhone"
end

declare predicate
  id localsWarnedBySMS
  name "LocalsWarnedBySMS"
end

declare predicate
  id localsWarnedByEmail
  name "LocalsWarnedByEmail"
end

declare predicate
  id dustyEnvironment
  name "DustyEnvironment"
end

declare predicate
  id falseEcho
  name "FalseEcho"
end

declare predicate
  id radarDepthSensorBroken
  name "DepthSensorBroken"
end

declare predicate
  id ultrasoundSensorBroken
  name "UltrasoundSensorBroken"
end

declare predicate
  id ultrasoundDistortion
  name "UltrasoundDistortion"
end

declare predicate
  id noisyImage
  name "NoisyImage"
end

declare predicate
  id gsmNetworkDown
  name "GSMNetworkDown"
end

declare predicate
  id voiceNetworkOverloaded
  name "VoiceNetworkOveloaded"
end

# Agents

declare agent
  id radarDepthSensor
  name "Depth Sensor"
end

declare agent
  id ultrasoundSensor
  name "Ultrasound Sensor"
end

declare agent
  id voiceWarner
  name "VoiceWarner"
end

declare agent
  id fds
  name "Flooding Detection System"
end

# Constraints

declare constraint
	id cst11
	name "Camera Speed Acquired and Acquired Radar Depth Accurate"
	conflict speed_acquired_by_camera, acquired_ultrasound_speed_accurate
end

declare constraint
	id cst12
	name "Camera Speed Acquired and Acquired Radar Depth Accurate 2"
	conflict speed_acquired_every_5_seconds, acquired_camera_speed_accurate
end

declare constraint
  id cst13
  conflict locals_warned_by_sms, locals_warned_by_email, locals_warned
end
